<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.7">
  <POU Name="MAIN" Id="{dccfa27a-810e-40bf-80c7-52538d0fa408}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	L1_ISI_GND_BRS_ITMX_TILT AT %MD0: INT;
	L1_ISI_GND_BRS_ITMX_DRIFT AT %MD1: INT;
	L1_ISI_GND_BRS_ITMX_VEL AT %MD2: INT;(*~ (OPC : 1 : visible for OPC-Server) 
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Sqrt(Velocity): DESC)
										(OPC_PROP[0103] :-500: LOPR)
										(OPC_PROP[0102] :+500: HOPR)
										*)
	L1_ISI_GND_BRS_ITMX_REF AT %MD3: INT;		
	L1_ISI_GND_BRS_ITMX_CPULSE AT %MD4: INT;
	L1_ISI_GND_BRS_ITMX_LIGHTSRC AT %MD5: INT;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Light Source Status: DESC)
										*)
	L1_ISI_GND_BRS_ITMX_CAMERA AT %MD6: INT;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Camera Status: DESC)
										*)
	
	L1_ISI_GND_BRS_ITMX_PULSECHECK: INT:=0;
	L1_ISI_GND_BRS_ITMX_DAMPTIM: REAL;
	L1_ISI_GND_BRS_ITMX_RELAY1: BOOL;
	L1_ISI_GND_BRS_ITMX_RELAY2: BOOL;
	L1_ISI_GND_BRS_ITMX_DRIFTBIT: BOOL;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Drift Status: DESC)
										(OPC_PROP[0106] :GOOD: ONAM)
										(OPC_PROP[0107] :BAD: ZNAM)
										*)
	L1_ISI_GND_BRS_ITMX_MODBIT: BOOL;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :ISI Modules Status: DESC)
										(OPC_PROP[0106] :GOOD: ONAM)
										(OPC_PROP[0107] :BAD: ZNAM)
										*)
	L1_ISI_GND_BRS_ITMX_BOXBIT: BOOL;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :BRS Box Status: DESC)
										(OPC_PROP[0106] :GOOD: ONAM)
										(OPC_PROP[0107] :BAD: ZNAM)
										*)
	L1_ISI_GND_BRS_ITMX_CAPDRIVE: INT;
		
	L1_ISI_GND_BRS_ITMX_TEMPL AT %IW0: INT;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Tempurature Left: DESC)
										(OPC_PROP[0100] :1/100 C: EGU)
										*)
	L1_ISI_GND_BRS_ITMX_TEMPR AT %IW1: INT;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :1: read-only)
										(OPC_PROP[0101] :Tempurature Right: DESC)
										(OPC_PROP[0100] :1/100 C: EGU)
										*)
	L1_ISI_GND_BRS_ITMX_BOXIN AT %IW2: BOOL;
	L1_ISI_GND_BRS_ITMX_MOD1IN AT %IW3: BOOL;
	L1_ISI_GND_BRS_ITMX_MOD2IN AT %IW4: BOOL;	
	L1_ISI_GND_BRS_ITMX_USER: BOOL:=TRUE;(*~ (OPC : 1 : visible for OPC-Server)
											(OPC_PROP[0005] :3: read/write)
											(OPC_PROP[0101] :User Damping Control: DESC)
											(OPC_PROP[0106] :On: ONAM)
											(OPC_PROP[0107] :Off: ZNAM)
											*)	
	L1_ISI_GND_BRS_ITMX_LOWTHRESHOLD: INT:=800;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :3: read/write)
										(OPC_PROP[0101] :Lower Damping Threshold: DESC)*)
	L1_ISI_GND_BRS_ITMX_HIGHTHRESHOLD: INT:=2000;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :3: read/write)
										(OPC_PROP[0101] :Upper Damping Threshold: DESC)*)
	L1_ISI_GND_BRS_ITMX_DAMPTIMEOUT: REAL:=62500;(*~ (OPC : 1 : visible for OPC-Server)
										(OPC_PROP[0005] :3: read/write)
										(OPC_PROP[0101] :Damping Timeout: DESC)*)
	
	L1_ISI_GND_BRS_ITMX_CAPOUTL AT %QW0: INT;
	L1_ISI_GND_BRS_ITMX_CAPOUTR AT %QW1: INT;
	L1_ISI_GND_BRS_ITMX_AMPBIT AT %QW3: BOOL;(*~ (OPC : 1 : visible for OPC-Server)
											(OPC_PROP[0005] :1: read-only)
											(OPC_PROP[0101] :Amplitude Status: DESC)
											(OPC_PROP[0106] :GOOD: ONAM)
											(OPC_PROP[0107] :BAD: ZNAM)
											*)
	L1_ISI_GND_BRS_ITMX_TILTOUT AT %QW4: INT;
	L1_ISI_GND_BRS_ITMX_DRIFTOUT AT %QW5: INT;
	L1_ISI_GND_BRS_ITMX_REFOUT AT %QW6: INT;
	L1_ISI_GND_BRS_ITMX_DAMPBIT AT %QW7: BOOL;(*~ (OPC : 1 : visible for OPC-Server)
											(OPC_PROP[0005] :1: read-only)
											(OPC_PROP[0101] :Damping Status: DESC)
											(OPC_PROP[0106] :Damping: ONAM)
											(OPC_PROP[0107] :Not Damping: ZNAM)
											*)
	
	L1_ISI_GND_BRS_ITMX_CBIT AT %QW8: BOOL; (*~ (OPC : 1 : visible for OPC-Server)
											(OPC_PROP[0005] :1: read-only)
											(OPC_PROP[0101] :C# Running Status: DESC)
											(OPC_PROP[0106] :GOOD: ONAM)
											(OPC_PROP[0107] :BAD: ZNAM)
											*)
	
	L1_ISI_GND_BRS_ITMX_RELAYL AT %QW9: BOOL;
	L1_ISI_GND_BRS_ITMX_RELAYR AT %QW10: BOOL;
	L1_ISI_GND_BRS_ITMX_NOTRELAYL AT %QW11: BOOL;
	L1_ISI_GND_BRS_ITMX_NOTRELAYR AT %QW12: BOOL;
	L1_ISI_GND_BRS_ITMX_STATUSOUT AT %QW13: INT;
	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*User master switch for damping*)
IF L1_ISI_GND_BRS_ITMX_USER=TRUE
THEN
	(*If damper is on. 3276 = 1V output*)
	IF L1_ISI_GND_BRS_ITMX_DAMPBIT=TRUE
	THEN
		(*If under lower threshold the damping timer turns on.*)
		IF ABS(L1_ISI_GND_BRS_ITMX_VEL)<L1_ISI_GND_BRS_ITMX_LOWTHRESHOLD
			(*Checks to see if damping timer is off and start Timer.*)
			THEN IF L1_ISI_GND_BRS_ITMX_DAMPTIM <=0
				THEN L1_ISI_GND_BRS_ITMX_DAMPTIM:=1;
					L1_ISI_GND_BRS_ITMX_CAPDRIVE:=L1_ISI_GND_BRS_ITMX_VEL;
				(*Damping timeout check*)
				ELSE IF L1_ISI_GND_BRS_ITMX_DAMPTIM<L1_ISI_GND_BRS_ITMX_DAMPTIMEOUT
					THEN 
					L1_ISI_GND_BRS_ITMX_CAPDRIVE:=L1_ISI_GND_BRS_ITMX_VEL;
					L1_ISI_GND_BRS_ITMX_DAMPTIM:=L1_ISI_GND_BRS_ITMX_DAMPTIM+1;
					(*Damper timeout reset*)
					ELSE
						L1_ISI_GND_BRS_ITMX_DAMPBIT:=FALSE;
						L1_ISI_GND_BRS_ITMX_CAPDRIVE:=0;
						L1_ISI_GND_BRS_ITMX_DAMPTIM:=0;
						L1_ISI_GND_BRS_ITMX_RELAY1:=FALSE;
						L1_ISI_GND_BRS_ITMX_RELAY2:=FALSE;
					END_IF
				END_IF
			(*If above lower threshold continue damping*)
			ELSE
				L1_ISI_GND_BRS_ITMX_DAMPTIM:=0;
				L1_ISI_GND_BRS_ITMX_CAPDRIVE:=L1_ISI_GND_BRS_ITMX_VEL;
			END_IF
			(*If capdrive is positive send to left capacitor, if negative send to right capacitor*)
			IF L1_ISI_GND_BRS_ITMX_CAPDRIVE >= 0
			THEN L1_ISI_GND_BRS_ITMX_RELAY1:=TRUE;
				L1_ISI_GND_BRS_ITMX_RELAY2:=FALSE;
				L1_ISI_GND_BRS_ITMX_CAPOUTL:=L1_ISI_GND_BRS_ITMX_CAPDRIVE;
				L1_ISI_GND_BRS_ITMX_CAPOUTR:=0;
			ELSE L1_ISI_GND_BRS_ITMX_RELAY2:=TRUE;
				L1_ISI_GND_BRS_ITMX_RELAY1:=FALSE;
				L1_ISI_GND_BRS_ITMX_CAPOUTR:=-L1_ISI_GND_BRS_ITMX_CAPDRIVE;
				L1_ISI_GND_BRS_ITMX_CAPOUTL:=0;
			END_IF	
	(*If damper is off*)
	ELSE 
		(*Upper threshold check.*)
		IF ABS(L1_ISI_GND_BRS_ITMX_VEL)>L1_ISI_GND_BRS_ITMX_HIGHTHRESHOLD
		(*Sets damper on.*)
		THEN L1_ISI_GND_BRS_ITMX_DAMPBIT:=TRUE;				
		(*If below upper threshold turn off damping*)
		ELSE 
			 L1_ISI_GND_BRS_ITMX_RELAY1:=TRUE;
			 L1_ISI_GND_BRS_ITMX_RELAY2:=TRUE;
			 IF L1_ISI_GND_BRS_ITMX_VEL=0
				 THEN L1_ISI_GND_BRS_ITMX_CAPDRIVE:=0;
				 ELSE
					L1_ISI_GND_BRS_ITMX_CAPDRIVE:= (L1_ISI_GND_BRS_ITMX_VEL/ABS(L1_ISI_GND_BRS_ITMX_VEL))*(L1_ISI_GND_BRS_ITMX_VEL*L1_ISI_GND_BRS_ITMX_VEL)/20/1000;
			 		L1_ISI_GND_BRS_ITMX_CAPOUTL:=L1_ISI_GND_BRS_ITMX_CAPDRIVE+1600;
					L1_ISI_GND_BRS_ITMX_CAPOUTR:=-L1_ISI_GND_BRS_ITMX_CAPDRIVE+1600;
			END_IF			 	
		END_IF
	END_IF	
(*User control check.*)
ELSE L1_ISI_GND_BRS_ITMX_RELAY1:=FALSE;
	L1_ISI_GND_BRS_ITMX_RELAY2:=FALSE;
	L1_ISI_GND_BRS_ITMX_CAPDRIVE:=0;
	L1_ISI_GND_BRS_ITMX_CAPOUTL:=0;
	L1_ISI_GND_BRS_ITMX_CAPOUTR:=0;
	L1_ISI_GND_BRS_ITMX_DAMPBIT:=FALSE;	
	
END_IF
(*Nonlinear amplitude check. Sets amplitude status bit.*)
IF ABS(L1_ISI_GND_BRS_ITMX_TILT)>10000
	THEN L1_ISI_GND_BRS_ITMX_AMPBIT:=FALSE;
ELSE L1_ISI_GND_BRS_ITMX_AMPBIT:=TRUE;
END_IF
(*Drift amplitude check.*)
IF ABS(L1_ISI_GND_BRS_ITMX_DRIFT)>=32000
	THEN L1_ISI_GND_BRS_ITMX_DRIFTBit:=FALSE;
ELSE L1_ISI_GND_BRS_ITMX_DRIFTBit:=TRUE;
END_IF
(*C# code pulse check. Sets C# code status bit.*)
IF L1_ISI_GND_BRS_ITMX_PULSECHECK>=10
	THEN L1_ISI_GND_BRS_ITMX_PULSECHECK:=0;
	IF L1_ISI_GND_BRS_ITMX_CPULSE=1
		THEN L1_ISI_GND_BRS_ITMX_CBIT:=TRUE;
		L1_ISI_GND_BRS_ITMX_CPULSE:=0;		
	ELSE L1_ISI_GND_BRS_ITMX_CBIT:=FALSE;		
	END_IF
END_IF

(*Beckhoff status inversion*)
L1_ISI_GND_BRS_ITMX_BOXBIT:=NOT L1_ISI_GND_BRS_ITMX_BOXIN;
L1_ISI_GND_BRS_ITMX_MODBIT:= NOT L1_ISI_GND_BRS_ITMX_MOD1IN OR NOT L1_ISI_GND_BRS_ITMX_MOD2IN;
IF (L1_ISI_GND_BRS_ITMX_CBIT=FALSE)  OR (L1_ISI_GND_BRS_ITMX_DRIFTBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_LIGHTSRC=0) OR (L1_ISI_GND_BRS_ITMX_CAMERA=0) OR (L1_ISI_GND_BRS_ITMX_MODBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_BOXBIT=FALSE)
	THEN 
		L1_ISI_GND_BRS_ITMX_CAPDRIVE:=0;
		L1_ISI_GND_BRS_ITMX_RELAY1:=FALSE;
		L1_ISI_GND_BRS_ITMX_RELAY2:=FALSE;
		L1_ISI_GND_BRS_ITMX_DAMPBIT:=FALSE;
END_IF
(*Overview status out construction. If any error flags are raised set status to 0V, else if damping set to 2 V, else set to 5V*)
IF (L1_ISI_GND_BRS_ITMX_CBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_AMPBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_DRIFTBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_LIGHTSRC=0) OR (L1_ISI_GND_BRS_ITMX_CAMERA=0) OR (L1_ISI_GND_BRS_ITMX_MODBIT=FALSE) OR (L1_ISI_GND_BRS_ITMX_BOXBIT=FALSE)
	THEN 
		L1_ISI_GND_BRS_ITMX_STATUSOUT:=0;
ELSE IF L1_ISI_GND_BRS_ITMX_DAMPBIT=TRUE
	THEN L1_ISI_GND_BRS_ITMX_STATUSOUT:=2*3276;
	ELSE
		L1_ISI_GND_BRS_ITMX_STATUSOUT:=5*3276;
	END_IF
END_IF
L1_ISI_GND_BRS_ITMX_PULSECHECK:=L1_ISI_GND_BRS_ITMX_PULSECHECK+1;
(*Relay states set so the capacitors are either passed the damping signal or grounded.*)
L1_ISI_GND_BRS_ITMX_RELAYL:= L1_ISI_GND_BRS_ITMX_RELAY1;
L1_ISI_GND_BRS_ITMX_RELAYR:= L1_ISI_GND_BRS_ITMX_RELAY2;
L1_ISI_GND_BRS_ITMX_NOTRELAYL:= NOT L1_ISI_GND_BRS_ITMX_RELAY1;
L1_ISI_GND_BRS_ITMX_NOTRELAYR:= NOT L1_ISI_GND_BRS_ITMX_RELAY2;

L1_ISI_GND_BRS_ITMX_TILTOUT:=L1_ISI_GND_BRS_ITMX_TILT;
L1_ISI_GND_BRS_ITMX_DRIFTOUT:=L1_ISI_GND_BRS_ITMX_DRIFT;
L1_ISI_GND_BRS_ITMX_REFOUT:=L1_ISI_GND_BRS_ITMX_REF;]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="3" Count="2" />
      <LineId Id="7" Count="45" />
      <LineId Id="55" Count="7" />
      <LineId Id="499" Count="0" />
      <LineId Id="498" Count="0" />
      <LineId Id="64" Count="9" />
      <LineId Id="95" Count="19" />
      <LineId Id="121" Count="3" />
      <LineId Id="490" Count="1" />
      <LineId Id="493" Count="2" />
      <LineId Id="489" Count="0" />
      <LineId Id="497" Count="0" />
      <LineId Id="125" Count="2" />
      <LineId Id="488" Count="0" />
      <LineId Id="130" Count="14" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>